plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.7.0'
    id "com.github.johnrengelman.shadow" version "7.1.2"
    id "java"
}


group 'al.aoli.exchain.instrumentation'
version 'unspecified'

repositories {
    mavenCentral()
    mavenLocal()
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
    implementation group: 'net.bytebuddy', name: 'byte-buddy-dep', version: '1.12.12'
    implementation group: 'net.bytebuddy', name: 'byte-buddy-agent', version: '1.12.12'
    implementation group: 'org.ow2.asm', name: 'asm', version: '9.3'
    implementation group: 'org.ow2.asm', name: 'asm-commons', version: '9.3'
    implementation group: 'org.ow2.asm', name: 'asm-analysis', version: '9.3'
    implementation group: 'io.github.microutils', name: 'kotlin-logging-jvm', version: '2.1.23'
    implementation group: 'org.slf4j', name: 'slf4j-simple', version: '1.7.36'
    compileOnly  'edu.gmu.swe.phosphor:Phosphor:0.1.0-SNAPSHOT'
}

test {
    useJUnitPlatform()
}

jar {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
    manifest {
        attributes 'Premain-Class': "al.aoli.exchain.instrumentation.MainKt"
        attributes 'Can-Redefine-Classes': true
        attributes 'Can-Retransform-Classes': true
    }
}

shadowJar {
    archiveBaseName.set('instrumentation')
    archiveClassifier.set('')
    archiveVersion.set('')

    relocate 'org.objectweb.asm', 'shadow.asm'
    dependsOn ':native:linkDebug'
}


task startInstrumentation(dependsOn: 'classes', type: JavaExec) {
    mainClass = "al.aoli.exchain.instrumentation.DoIntegrationTestKt"
    classpath = sourceSets.main.runtimeClasspath
}

