import com.github.jengelman.gradle.plugins.shadow.tasks.ConfigureShadowRelocation

plugins {
	id "java"
	id 'org.jetbrains.kotlin.jvm'
	id "com.github.johnrengelman.shadow" version "7.1.2"
}


group 'al.aoli.exchain.instrumentation'
version 'unspecified'

dependencies {
	compileOnly project(path: ":runtime", configuration: "shadow")
	testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
	testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
	implementation group: 'net.bytebuddy', name: 'byte-buddy-dep', version: '1.12.12'
	implementation group: 'net.bytebuddy', name: 'byte-buddy-agent', version: '1.12.12'

}

test {
	useJUnitPlatform()
}

jar {
	duplicatesStrategy = DuplicatesStrategy.INCLUDE
	var runtime = project(":runtime")
	manifest {
		attributes 'Premain-Class': "al.aoli.exchain.instrumentation.MainKt"
		attributes 'Can-Redefine-Classes': true
		attributes 'Can-Retransform-Classes': true
		attributes 'Class-Path': "${runtime.buildDir}/libs/out.jar"
	}
}

shadowJar {
	archiveBaseName.set('instrumentation')
	from { project.configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) } }
}

task startInstrumentation(dependsOn: 'classes', type: JavaExec) {
	mainClass = "al.aoli.exchain.instrumentation.DoIntegrationTestKt"
	classpath = sourceSets.main.runtimeClasspath
}
