import com.github.jengelman.gradle.plugins.shadow.tasks.ConfigureShadowRelocation

plugins {
    id "java"
    id 'org.jetbrains.kotlin.jvm'
    id "com.github.johnrengelman.shadow" version "7.1.2"
}


group 'al.aoli.exchain.instrumentation'
version 'unspecified'

repositories {
    mavenCentral()
    mavenLocal()
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
    implementation group: 'net.bytebuddy', name: 'byte-buddy-dep', version: '1.12.12'
    implementation group: 'net.bytebuddy', name: 'byte-buddy-agent', version: '1.12.12'
    compileOnly project(path: ":runtime", configuration: "shadow")
}

test {
    useJUnitPlatform()
}

jar {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
    manifest {
        attributes 'Premain-Class': "al.aoli.exchain.instrumentation.MainKt"
        attributes 'Can-Redefine-Classes': true
        attributes 'Can-Retransform-Classes': true
    }
}

shadowJar {
    archiveBaseName.set('instrumentation')
    archiveClassifier.set('')
    archiveVersion.set('')

    relocate 'org.objectweb.asm', 'shadow.asm'
}

task startInstrumentation(dependsOn: 'classes', type: JavaExec) {
    mainClass = "al.aoli.exchain.instrumentation.DoIntegrationTestKt"
    classpath = sourceSets.main.runtimeClasspath
}

