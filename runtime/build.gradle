import org.gradle.internal.jvm.Jvm
import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar

plugins {
	id 'org.jetbrains.kotlin.jvm'
	id "com.github.johnrengelman.shadow" version "7.1.2"
}

group 'al.aoli.exchain.runtime'
version 'unspecified'

dependencies {
	testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
	testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
	implementation group: 'org.ow2.asm', name: 'asm', version: '9.3'
	implementation group: 'org.ow2.asm', name: 'asm-commons', version: '9.3'
	implementation group: 'org.ow2.asm', name: 'asm-analysis', version: '9.3'
	implementation group: 'com.google.code.gson', name: 'gson', version: '2.9.1'
	implementation("com.github.ajalt.mordant:mordant:2.0.0-beta7")
	compileOnly 'edu.gmu.swe.phosphor:phosphor-instrument-jigsaw:0.1.0-SNAPSHOT'
}

test {
	useJUnitPlatform()
}


task createInstrumentedJDK(type: JavaExec) {
	var phosphor = project(":phosphor")
	classpath "$phosphor.buildDir/libs/phosphor-merged.jar"
	mainClass = "edu.columbia.cs.psl.jigsaw.phosphor.instrumenter.StandaloneJVMInstrumenter"
	var src = Jvm.current().javaHome
	var dst = "$buildDir/jre-inst"
	args src, dst, "--jvmModules", "ALL-MODULE-PATH",
			"-taintTagFactory", "al.aoli.exchain.phosphor.instrumenter.DynamicSwitchTaintTagFactory"
	dependsOn ":phosphor:mergedJar"
}

jar {
	archiveClassifier.set("slim")
}

task createShadow(type: ShadowJar) {
	duplicatesStrategy = DuplicatesStrategy.EXCLUDE
	archiveBaseName.set('runtime-shadow')
	archiveClassifier.set('')
	archiveVersion.set('')
	relocate 'org.objectweb.asm', 'al.aoli.exchain.instrumentation.asm'
	dependsOn ':native:build'
	exclude "module-info.class"
	from sourceSets.main.output
	from { project.configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) } }
	manifest {
		attributes "Premain-Class": "al.aoli.exchain.runtime.PreMainKt"
	}
	dependsOn createInstrumentedJDK
}
